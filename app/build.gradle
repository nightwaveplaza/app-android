plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Load acra local properties
Properties acraProperties = new Properties()
if (project.rootProject.file('acra.properties').canRead()) {
    acraProperties.load(project.rootProject.file('acra.properties').newDataInputStream())
}
// Load acra local properties
Properties plazaProperties = new Properties()
if (project.rootProject.file('plaza.properties').canRead()) {
    plazaProperties.load(project.rootProject.file('plaza.properties').newDataInputStream())
}

android {
    namespace 'one.plaza.nightwaveplaza'
    compileSdk 35

    defaultConfig {
        applicationId 'one.plaza.nightwaveplaza'
        minSdk 21
        targetSdkVersion 35
        versionCode 231
        versionName '2.1.0'

        buildFeatures {
            buildConfig true
        }

        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField 'String', 'ACRA_URI', acraProperties.getProperty('ACRA_URI', '""')
        buildConfigField 'String', 'ACRA_LOGIN', acraProperties.getProperty('ACRA_LOGIN', '""')
        buildConfigField 'String', 'ACRA_PASS', acraProperties.getProperty('ACRA_PASS', '""')
        buildConfigField 'String', 'PLAZA_API', plazaProperties.getProperty('PLAZA_API', '""')
        buildConfigField 'String', 'PLAZA_URL_OVERRIDE', plazaProperties.getProperty('PLAZA_URL_OVERRIDE', '""')
    }

    signingConfigs {
        release {
            storeFile file('keystore/upload.jks')
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('RELEASE_SIGN_KEY_ALIAS')
            keyPassword System.getenv('RELEASE_SIGN_KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            shrinkResources true
            signingConfig signingConfigs.release
            postprocessing {
                removeUnusedCode true
                obfuscate false
                optimizeCode true
            }
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    bundle {
        language {
            enableSplit = false
        }
    }
}

dependencies {
    implementation 'com.google.guava:guava:33.3.1-android' // for Android 5/6 compatibility
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.23')
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.activity:activity-compose:1.10.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation platform('androidx.compose:compose-bom:2025.03.01')

    implementation 'androidx.media3:media3-exoplayer:1.6.0'
    implementation 'androidx.media3:media3-session:1.6.0'
    implementation 'androidx.media3:media3-exoplayer-hls:1.6.0'

    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'androidx.webkit:webkit:1.13.0'

    implementation 'com.github.bumptech.glide:glide:4.16.0'

    implementation 'ch.acra:acra-http:5.11.3'

    implementation ('io.socket:socket.io-client:2.1.2') {
        exclude group: 'org.json', module: 'json'
    }

    implementation 'com.jakewharton.timber:timber:5.0.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'
}

tasks.register('printVersionName') {
    println (android.defaultConfig.versionName + "-b" + android.defaultConfig.versionCode)
}