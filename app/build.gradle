plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Load acra local properties
Properties acraProperties = new Properties()
if (project.rootProject.file('acra.properties').canRead()) {
    acraProperties.load(project.rootProject.file('acra.properties').newDataInputStream())
}

android {
    namespace 'one.plaza.nightwaveplaza'
    compileSdk 34

    defaultConfig {
        applicationId 'one.plaza.nightwaveplaza'
        minSdk 21
        targetSdk 34
        versionCode 218
        versionName '2.0.4'

        buildFeatures {
            buildConfig true
        }

        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField 'String', 'ACRA_URI', acraProperties.getProperty('ACRA_URI', '""')
        buildConfigField 'String', 'ACRA_LOGIN', acraProperties.getProperty('ACRA_LOGIN', '""')
        buildConfigField 'String', 'ACRA_PASS', acraProperties.getProperty('ACRA_PASS', '""')
    }

    signingConfigs {
        release {
            storeFile file('keystore/upload.jks')
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('RELEASE_SIGN_KEY_ALIAS')
            keyPassword System.getenv('RELEASE_SIGN_KEY_PASSWORD')
        }
    }


    buildTypes {
        release {
            shrinkResources true
            signingConfig signingConfigs.release
            postprocessing {
                removeUnusedCode true
                obfuscate false
                optimizeCode true
            }
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'com.google.guava:guava:32.1.3-android' // for Android 5/6 compatibility
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.23')
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation platform('androidx.compose:compose-bom:2024.03.00')

    implementation 'androidx.media3:media3-exoplayer:1.3.1'
    implementation 'androidx.media3:media3-session:1.3.1'
    implementation 'androidx.media3:media3-exoplayer-hls:1.3.1'


    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'androidx.webkit:webkit:1.10.0'

    implementation 'com.github.bumptech.glide:glide:4.16.0'

    implementation 'ch.acra:acra-http:5.11.3'
}

tasks.register('printVersionName') {
    println (android.defaultConfig.versionName + "-b" + android.defaultConfig.versionCode)
}